# Raet Test FloScript

house master

init .raet.udp.stack.local to did 1 name "master" host "" port 7530
init .raet.uxd.stack.local to name "boss" lane "ash" yid 0

framer masterudpstack be active first start
   frame start
      do raet udp stack per inode ".raet.udp.stack"
      exit
         do raet udp stack closer per inode ".raet.udp.stack."

framer receiver be active first start
   frame start
      do raet udp stack printer per inode ".raet.udp.stack."
      timeout 5

   frame abort
      bid stop all

framer bossuxdstack be active first start
   frame start
      do raet uxd stack per inode ".raet.uxd.stack"
      exit
         do raet uxd stack closer per inode ".raet.uxd.stack."

framer uxdreceiver be active first start
   frame start
      do raet uxd stack printer per inode ".raet.udp.stack."
      timeout 5

   frame abort
      bid stop me

framer setupuxdyard be active first start
   frame start
      enter
         do raet uxd stack yard add to lane "ash" yid 1

   frame send
      enter
         do raet uxd stack transmit to stuff "Lord Hello" \
            per inode ".raet.uxd.stack."

   frame stop
      bid stop me


house serf

init .raet.uxd.stack.local to name "serf" lane "ash" yid 1

framer serfuxdstack be active first start
   frame start
      do raet uxd stack per inode ".raet.uxd.stack"
      exit
         do raet uxd stack closer per inode ".raet.uxd.stack."

framer uxdreceiver be active first start
   frame start
      do raet uxd stack printer per inode ".raet.udp.stack."
      timeout 5

   frame abort
      bid stop me


framer setupuxdyard be active first start
   frame start
      enter
         do raet uxd stack yard add to lane "ash" yid 0
      timeout 0.5

   frame send
      enter
         do raet uxd stack transmit to stuff "Serf Hello" \
            per inode ".raet.uxd.stack."

   frame stop
      bid stop me


house minion1

init .raet.udp.stack.local to did 0 name "minion1" host "" port 7531


framer minionudpstack be active first start
   frame start
      do raet udp stack per inode ".raet.udp.stack"
      exit
         do raet udp stack closer per inode ".raet.udp.stack."

framer bootstrap be active first join
   frame join
      print Joining Minion1...
      enter
         do raet udp stack joiner per inode ".raet.udp.stack."
      recur
         do raet udp stack joined per inode ".raet.udp.stack."

      go next if joined in .raet.udp.stack.status
      go abort if elapsed >= 5

   frame joined
      print Joined Minion1
      go next

   frame allow
      print Allowing Minion1...
      enter
         do raet udp stack allower per inode ".raet.udp.stack."
      recur
         do raet udp stack allowed per inode ".raet.udp.stack."

      go next if allowed in .raet.udp.stack.status
      go abort if elapsed >= 5


   frame allowed
      print Allowed Minion1
      go next

   frame message
      print Messaging Minion1...
      enter
         do raet udp stack messenger to contents "Minion 1 Hello" code 15 \
            per inode ".raet.udp.stack."
      go next

   frame idle
      print Idling Minion1...
      do raet udp stack idled per inode ".raet.udp.stack."
      go abort if idled in .raet.udp.stack.status

   frame abort
      bid stop all

