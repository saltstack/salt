# Raet Test FloScript

house minion

init .raet.udp.stack.local to did 0 name "minion" host "" port 7531
init port in .raet.udp.stack.local from value in .salt.etc.raet_port


framer minionudpstack be active first start
   frame start
      do raet udp stack per inode ".raet.udp.stack"
      exit
         do raet udp stack closer per inode ".raet.udp.stack."

framer bootstrap be active first join
   frame join
      print Joining...
      enter
         do raet udp stack joiner per inode ".raet.udp.stack."
      recur
         do raet udp stack joined per inode ".raet.udp.stack."

      go next if joined in .raet.udp.stack.status
      go abort if elapsed >= 10

   frame joined
      print Joined
      go next

   frame allow
      print Allowing...
      enter
         do raet udp stack allower per inode ".raet.udp.stack."
      recur
         do raet udp stack allowed per inode ".raet.udp.stack."

      go next if allowed in .raet.udp.stack.status
      go abort if elapsed >= 5


   frame allowed
      print Allowed
      go next

   frame message
      print Messaging...
      enter
         do raet udp stack messenger to contents "Minion 1 Hello" code 15 \
            per inode ".raet.udp.stack."
      go next

   frame idle
      print Idling...
      do raet udp stack idled per inode ".raet.udp.stack."
      go abort if idled in .raet.udp.stack.status

   frame abort
      bid stop all
